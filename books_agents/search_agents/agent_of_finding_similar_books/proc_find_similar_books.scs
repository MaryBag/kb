scp_program -> proc_find_similar_books
(*
    -> rrel_params: ...
    (*
        -> rrel_1: rrel_in: _book;;
        -> rrel_2: rrel_in: _book_author;;
        -> rrel_3: rrel_in: _similar_books;;
    *);;

    -> rrel_operators: ...
    (*
    	
        -> rrel_init: .proc_find_similar_books_operator_print_start
        (*
            <- printNl;;

            -> rrel_1: rrel_scp_const: [Начало. Процедура нахождения схожих книг];;

            => nrel_goto: .proc_find_similar_books_operator_find_book_author;;
        *);;


        // Проверка наличия автора

    	-> .proc_find_similar_books_operator_find_book_author
        (*
            <- ifVarAssign;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _book_author;;

            => nrel_then: .proc_find_similar_books_operator_find_author_all_books;;
            => nrel_else: .proc_find_similar_books_operator_print_end;;
        *);;

		-> .proc_find_similar_books_operator_find_author_all_books
		(*
			<- call;;

			-> rrel_1: rrel_fixed: rrel_scp_const: proc_find_books_of_author;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ...
			(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _book_author;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _similar_books;;
			*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _proc_find_author_books_descriptor;;

			=> nrel_goto: .proc_find_similar_books_operator_find_author_all_books_wait;;
		*);;

		-> .proc_find_similar_books_operator_find_author_all_books_wait
		(*
			<- waitReturn;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _proc_find_author_books_descriptor;;

			=> nrel_goto: .proc_find_similar_books_operator_elements_books_found;;
		*);;


        // Поиск исходной книги во множестве схожих книг        

        -> .proc_find_similar_books_operator_elements_books_found
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _similar_books;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _temp_arc_book_in_similar;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _book;;

	   => nrel_then: .proc_find_similar_books_operator_remove_the_book_2;;

            => nrel_else: .proc_find_similar_books_operator_find_genre_all_books;;
        *);;


        -> .proc_find_similar_books_operator_remove_the_book_2
        (*
            <- eraseEl;;

           -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _temp_arc_book_in_similar;;

            => nrel_goto: .proc_find_similar_books_operator_find_genre_all_books;;
        *);;

        //Поиск жанра запрашиаваемой книги

        -> .proc_find_similar_books_operator_find_genre_all_books
        (*
           <- searchSetStr3;;

            -> rrel_1: rrel_assign: rrel_scp_var: _super_set;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _book;;

            -> rrel_set_1: rrel_assign: rrel_scp_var: _book_super_set;;

            => nrel_then: .proc_find_similar_books_operator_find_genres_01;;
            => nrel_else: .proc_find_similar_books_operator_print_end;;
        *);;

        -> .proc_find_similar_books_operator_find_genres_01
        (*
           <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _book_super_set;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _temp_elem_arc;;
            -> rrel_3: rrel_assign: rrel_scp_var: _book_genre;;

            => nrel_then: .proc_find_similar_books_operator_find_genres_02;;
            => nrel_else: .proc_find_similar_books_operator_print_end;;
        *);;

        -> .proc_find_similar_books_operator_find_genres_02
        (*
            <- eraseEl;;

           -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _temp_elem_arc;;

            => nrel_goto: .proc_find_similar_books_operator_find_genres_03;;
        *);;

         -> .proc_find_similar_books_operator_find_genres_03
        (*
           <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_const: genre;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _book_genre;;

            => nrel_then: .proc_find_similar_books_operator_find_genres_1;;
            => nrel_else: .proc_find_similar_books_operator_find_genres_01;;
        *);;

	    -> .proc_find_similar_books_operator_find_genres_1
        (*
            <- searchSetStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _book_genre;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
            -> rrel_3: rrel_assign: rrel_scp_var: _book_similar_genre;;

            -> rrel_set_3: rrel_assign: rrel_scp_var: _books_with_similar_genre;;

            => nrel_goto: .proc_find_similar_books_operator_loop_to_find_book_in_specific_genre;;
        *);;

       
        -> .proc_find_similar_books_operator_loop_to_find_book_in_specific_genre
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _books_with_similar_genre;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _temp_genre_book_arc;;
            -> rrel_3: rrel_assign: rrel_scp_var: _similar_genre_book;;

            => nrel_then: .proc_find_similar_books_operator_remove_the_book_3;;

            => nrel_else: .proc_find_similar_books_operator_print_end;;
        *);;

        
        -> .proc_find_similar_books_operator_remove_the_book_3
        (*
            <- eraseEl;;

           -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _temp_genre_book_arc;;

            => nrel_goto: .proc_find_similar_books_operator_find_genres_3;;
        *);;

        -> .proc_find_similar_books_operator_find_genres_3
        (*
            <- ifCoin;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _book;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _similar_genre_book;;

            => nrel_then: .proc_find_similar_books_operator_loop_to_find_book_in_specific_genre;;
            => nrel_else: .proc_find_similar_books_operator_check_exist;;

        *);;

      
        -> .proc_find_similar_books_operator_check_exist
        (*
            <- searchSetStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _similar_books;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _temp_book_arc;;
            -> rrel_3: rrel_assign: rrel_scp_var: _curr_similar_book;;

            -> rrel_set_3: rrel_assign: rrel_scp_var: _temp_similar_books;;
			=> nrel_then: .proc_find_similar_books_operator_check_exist_2;;
			=> nrel_else: .proc_find_similar_books_operator_add_to_similar_first_book;;
        *);;
         -> .proc_find_similar_books_operator_check_exist_2
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _temp_similar_books;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _temp_curr_book_arc_in_similar;;
            -> rrel_3: rrel_assign: rrel_scp_var: _curr_temp_similar_book;;

			=> nrel_then: .proc_find_similar_books_operator_check_exist_3;;
			=> nrel_else: .proc_find_similar_books_operator_add_to_similar;;
        *);;
         -> .proc_find_similar_books_operator_check_exist_3
        (*
            <- eraseEl;;

           -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _temp_curr_book_arc_in_similar;;

            => nrel_goto: .proc_find_similar_books_operator_check_exist_4;;
        *);;
        -> .proc_find_similar_books_operator_check_exist_4
        (*
            <- ifCoin;;
             -> rrel_1: rrel_fixed: rrel_scp_var: _curr_temp_similar_book;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _similar_genre_book;;
             => nrel_then: .proc_find_similar_books_operator_loop_to_find_book_in_specific_genre;;
            => nrel_else: .proc_find_similar_books_operator_check_exist_2;;
         *);;


        -> .proc_find_similar_books_operator_add_to_similar_first_book
        (*
            <- genElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _similar_books;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _temp_book_arc;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _similar_genre_book;;

	    => nrel_goto: .proc_find_similar_books_operator_loop_to_find_book_in_specific_genre;;
        *);;

        -> .proc_find_similar_books_operator_add_to_similar
        (*
            <- genElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _similar_books;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _temp_book_arc;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _similar_genre_book;;

	    => nrel_goto: .proc_find_similar_books_operator_loop_to_find_book_in_specific_genre;;
        *);;
        

///////////////////////////////////////////////////////////////////////////////////
    //Формирование ответа

        -> .proc_find_similar_books_operator_print_end
        (*
            <- printNl;;

            -> rrel_1: rrel_scp_const: [Конец обработки. Процедура поиска схожих книг];;

            => nrel_goto: .proc_find_similar_books_operator_print_result;;
        *);;
         -> .proc_find_similar_books_operator_print_result
        (*
            <- printEl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _similar_books;;

            => nrel_goto: .proc_find_similar_books_operator_return;;
        *);;
        
    	-> .proc_find_similar_books_operator_return
    	(*
    		<- return;;
    	*);;
    *);;
*);;
